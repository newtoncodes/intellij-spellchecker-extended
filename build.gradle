plugins {
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'org.jetbrains.intellij' version '0.3.12'
    id 'java-gradle-plugin'
    id 'maven'
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

//

version = '0.2.0'
group = 'com.newtoncodes'
description = 'Shareable spell checker dictionaries for IntelliJ with Additional bundled hunspell dictionaries.'

//

sourceCompatibility = 1.8
targetCompatibility = 1.8

plugins.withType(JavaPlugin) {
    project.tasks.withType(GroovyCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    maven { url 'https://cache-redirector.jetbrains.com/jetbrains.bintray.com/intellij-plugin-service' }
    maven { url 'https://cache-redirector.jetbrains.com/repo1.maven.org/maven2' }
    maven { url 'https://cache-redirector.jetbrains.com/jcenter.bintray.com' }
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile 'org.jetbrains:annotations:13.0'
    compile 'org.jetbrains.intellij:plugin-repository-rest-client:0.4.30'
    compile 'org.jetbrains.intellij.plugins:structure-base:3.45'
    compile 'org.jetbrains.intellij.plugins:structure-intellij:3.45'
    compile 'de.undercouch:gradle-download-task:3.4.2'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }

    test {
        java.srcDirs = ['tests']
    }
}

intellij {
    version = '2018.3'
    downloadSources = true
    // updateSinceUntilBuild = false

    pluginName 'spellchecker-extended'
    plugins 'hunspell:183.3795.24'
    sandboxDirectory = "$project.buildDir/sandbox"
}

patchPluginXml {
    version = "$version"
    // pluginDescription description
    // changeNotes changelog
}

pluginBundle {
    website = 'https://github.com/newtoncodes/intellij-spellchecker-extended'
    vcsUrl = 'https://github.com/newtoncodes/intellij-spellchecker-extended'
    tags = ['spellchecker', 'dictionaries', 'dictionary', 'sync', 'share']
    description description

    //noinspection GroovyAssignabilityCheck
    plugins {
        intellijPlugin {
            id = 'com.newtoncodes.spellchecker'
            displayName = 'Spell checker extended'
        }
    }
}

publishPlugin {
    username intellijPublishUsername
    password intellijPublishPassword
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

wrapper {
    gradleVersion = '4.10'
    distributionUrl = "https://cache-redirector.jetbrains.com/services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
